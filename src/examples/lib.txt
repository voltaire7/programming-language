[-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-]
[ ASSIGNMENT                                                                                      ]
[-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-]

proc [symbol] [
    item-in 1 symbol _
] item-in 0 [as] _

proc [] [
    item-in 2 [_]
] as [return]

proc [] [
    item-in 0
] as [item]

proc [] [
    item-in 1 [_]
] as [ch]

[-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-]
[ LOOPS                                                                                           ]
[-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-]

proc [init cond incr body] [
    do-here init
    label [loop]
    do-here cond ? [
        do-here body
        do-here incr

        goto loop
    ] []
] as [for]

proc [cond body] [
    label [loop]
    do-here cond ? [
        do-here body

        goto loop
    ] []
] as [while]

proc [cond body] [
    label [loop]
    do-here body
    do-here cond ? [goto loop] []
] as [do-while]

proc [body] [
    label [loop]
    do-here body
    ? [goto loop] []
] as [loop]

proc [it body] [
    label [top]
    do-here body
    -1 + it as [it] ? [goto top] []
] as [repeat]

[-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-]
[ SYSCALLS                                                                                        ]
[-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-]

item [stdin]  0
item [stdout] 1
item [stderr] 2

item [SYS_EXIT]  1
item [SYS_READ]  3
item [SYS_WRITE] 4

proc [exit-code] [
    syscall SYS_EXIT [exit-code]
] as [exit]

proc [fd buf nbytes] [
    syscall SYS_READ [fd buf nbytes]
] as [read]

proc [fd buf nbytes] [
    syscall SYS_WRITE [fd buf nbytes]
] as [write]


